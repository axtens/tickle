   REM THE ZIPSET COMMAND FILE
   REM VERSION 1.0
   REM VERSION 1.1 ADDED .MKDIR C:\WPDOC
   REM  AND NEW ZIPSET DISK (7)
   REM VERSION 1.2 ADDED .IF &&1 AT VARIOUS POINTS TO RETURN THE ERRORLEVEL
   REM  OF THE PREVIOUS INVOKE
   REM VERSION 1.3 ADDED .IF !<VARNAME> <VALUE>
   REM VERSION 1.4 ADDED .VERIFY
   REM VERSION 1.5 ADDED MENU AND .BOX
   REM VERSION 1.6 ADDED MORE CONTROL
   REM VERSION 1.7 COMMENTED OUT REFERENCES TO DSKCACHE.SYS
   REM VERSION 1.8 NO MORE . TO MARK COMMANDS AND IMPROVED VARIABLE HANDLING
   REM VERSION 1.9 NOW AS .TCL FILE WITH VARS AS STRINGS
   REM VERSION 2.0 /V ON COMMANDLINE ADDS VERIFY
   REM             /B ON COMMANDLINE ADDS USERBREAK
   REM VERSION 2.1 MODIFICATIONS TO PERMIT GREATER CONTROL OVER ARCHIVING
   REM  SYSTEM, AND DISPLAY OF MENU OPTIONS AFTER CONFIGURATION
   REM VERSION 2.2 Added indentation and case changes to statements

   VAR ver 2
   VAR upd 1
   NOTIFY "ZIPSET Version &'VAR ver'.&'VAR upd'"

   REM If changing the archive file type, the archiver or extraction command
   REM  then change the values of the variables arcext, arcprg and arccmd.

   VAR arcext LZH
   VAR arcprg C:\LHARC.EXE
   VAR arccmd e

   REM define number of disks in ZipSet. Change zipdsk to
   REM  increase/decrease number of disks in ZipSet.
   VAR zipdsk 8
   VAR dskchk VAR zipdsk
   VAR cnt 0

   VAR opt " "
   VAR scr " "
   VAR pc  " "
   VAR clk " "

   VAR dosnam DOS401

LABEL MAINLOOP

LABEL MENU1
   CLS
   BOX 1 1 23 79
   SHOWXY 1 30 "SYSTEM CONFIGURATION"
   SHOWXY 3 5 "SPECIFY SCREEN TYPE FOR NEW MACHINE"
   SHOWXY 5 5 "1. MONO"
   SHOWXY 6 5 "2. CGA"
   SHOWXY 7 5 "3. EGA"
   SHOWXY 8 5 "4. EGA WITH MONO MONITOR"
   SHOWXY 9 5 "5. VGA"
   SHOWXY 11 5 "CHOICE: "
   GOTOXY 11 12

   GETCHAR opt
   IF VAR opt NI "12345"
      GOTO MENU1
   ENDIF
   PUTCHAR opt
   GOTO SCR&'VAR opt'

LABEL SCR1
   VAR scr MONO
   GOTO SCREND

LABEL SCR2
   VAR scr CGA
   GOTO SCREND

LABEL SCR3
   VAR scr EGA
   GOTO SCREND

LABEL SCR4
   VAR scr EGAMON
   GOTO SCREND

LABEL SCR5
   VAR scr VGA

LABEL SCREND
LABEL MENU2

   WIPE 3 5 11 60
   SHOWXY 3 5 "SPECIFY PC TYPE"
   SHOWXY 5 5 "1. XT"
   SHOWXY 6 5 "2. AT"
   SHOWXY 8 5 "CHOICE: "
   GOTOXY 8 12
   GETCHAR opt
   IF VAR opt NI "12"
        GOTO MENU2
   ENDIF
   PUTCHAR opt
   GOTO MACH&'VAR opt'

LABEL MACH1
   VAR pc XT
   GOTO MACHEND

LABEL MACH2
   VAR pc AT

LABEL MACHEND
LABEL MENU3

   WIPE 3 5 11 60
   SHOWXY 3 5 "SPECIFY DOS VERSION"
   SHOWXY 5 5 "1. MSDOS 3.21"
   SHOWXY 6 5 "2. MSDOS 3.3"
   SHOWXY 7 5 "3. MSDOS 4.01"
   SHOWXY 9 5 "CHOICE: "
   GOTOXY 9 12
   GETCHAR opt
   IF VAR opt NI "123"
        GOTO MENU3
   ENDIF
   PUTCHAR opt
   GOTO DOS&'VAR opt'

LABEL DOS1
   VAR dosnam DOS321
   GOTO ENDDOS

LABEL DOS2
   VAR dosnam DOS33
   GOTO ENDDOS

LABEL DOS3
   VAR dosnam DOS401

LABEL ENDDOS
LABEL MENU4

   REM This code could be used to bypass the 'Specify Clock Software'
   REM  menu when pc=AT.

   REM IF VAR pc EQ AT
   REM VAR clk " "
   REM GOTO MENU5
   REM ENDIF

   WIPE 3 5 11 60
   SHOWXY 3 5 "SPECIFY CLOCK SOFTWARE"
   SHOWXY 5 5 "1. CLOCK.COM"
   SHOWXY 6 5 "2. RAMCLK.COM"
   SHOWXY 7 5 "3. NO CLOCK"
   SHOWXY 10 5 "CHOICE: "
   GOTOXY 10 12
   GETCHAR opt
   IF VAR opt NI "123"
        GOTO MENU4
   ENDIF
   PUTCHAR opt
   GOTO CLOCK&'VAR opt'

LABEL CLOCK1
   VAR clk CLOCK.COM
   GOTO ENDCLOCK

LABEL CLOCK2
   VAR clk RAMCLK.COM
   GOTO ENDCLK

LABEL CLOCK3
   VAR clk " "

LABEL ENDCLOCK
LABEL MENU5

   WIPE 3 5 11 60
   SHOWXY 3 5 "SCREEN: &'VAR scr'"
   SHOWXY 4 5 "PC    : &'VAR pc'"
   SHOWXY 5 5 "DOS   : &'VAR dosnam'"
   SHOWXY 6 5 "CLOCK : &'VAR clk'"
   GOTOXY 11 5
   YESNO "CORRECT SETTINGS (Y/N)? "
   IF SYS YESNO EQ 0
        GOTO MAINLOOP
   ENDIF

   SHELL "COPY A:*.&'VAR arcext' C:\"
   SHELL "COPY A:VOPT.EXE C:\"

   REM CREATE SUBDIRECTORIES

   CHDIR "C:\"
   MKDIR "C:\DOS"
   MKDIR "C:\GAMES"
   MKDIR "C:\COMPOSER"
   MKDIR "C:\PCO"
   MKDIR "C:\PCFILE"
   MKDIR "C:\ASEASY"
   MKDIR "C:\GT"
   MKDIR "C:\UTILS"
   MKDIR "C:\TUTOR"
   MKDIR "C:\FASTYPE"
   MKDIR "C:\MENU"
   MKDIR "C:\PCW"
   MKDIR "C:\VDE"
   MKDIR "C:\WPDOC"

   REM COPY ARCHIVED FILES FROM FLOPPY DISK SET

   INCVAR dskchk
   VAR cnt 2

LABEL COPYLOOP
   CLS
   PAUSE "PLACE ZIPSET (&'VAR cnt') IS DRIVE A: AND PRESS ANY KEY ... "
   SHELL "COPY A:\ C:\"
   INCVAR cnt
   IF VAR cnt NE VAR dskchk
   GOTO COPYLOOP
   ENDIF

   REM If the number of archives files is to be changed then change
   REM  the value of arcnum to reflect increase/decrease.

   VAR arcnum 18
   VAR arclim VAR arcnum
   INCVAR arclim
   VAR cnt 1

   REM define archive names and destination paths
   REM Further disks should be kept in sequence and added at the end
   REM  of the following list.
   REM If removing disks, remember to keep the order contiguous, ie.
   REM  don't have gaps in the sequence.

   VAR arc1 C:\&'VAR dosnam'-1
   VAR sub1 C:\DOS\
   VAR arc2 C:\&'VAR dosnam'-2
   VAR sub2 C:\DOS\
   VAR arc3 C:\MENU
   VAR sub3 C:\MENU\
   VAR arc4 C:\SEETREE
   VAR sub4 C:\DOS\
   VAR arc5 C:\PCFILE
   VAR sub5 C:\PCFILE\
   VAR arc6 C:\ASEASY
   VAR sub6 C:\ASEASY\
   VAR arc7 C:\PCOUTLN
   VAR sub7 C:\PCO\
   VAR arc8 C:\GTSTART
   VAR sub8 C:\GT\
   VAR arc9 C:\GTRUN
   VAR sub9 C:\GT\
   VAR arc10 C:\UTILITY
   VAR sub10 C:\UTILS\
   VAR arc11 C:\GAMES
   VAR sub11 C:\GAMES\
   VAR arc12 C:\PCTUTOR
   VAR sub12 C:\TUTOR\
   VAR arc13 C:\FASTYPE
   VAR sub13 C:\FASTYPE\
   VAR arc14 C:\SCREEN
   VAR sub14 C:\
   VAR arc15 C:\COMPOSER
   VAR sub15 C:\COMPOSER\
   VAR arc16 C:\PCWRITE
   VAR sub16 C:\PCW\
   VAR arc17 C:\VDE
   VAR sub17 C:\VDE\
   VAR arc18 C:\WPDOC
   VAR sub18 C:\WPDOC\

   REM UNARCHIVE FILES TO SUBDIRECTORIES

   CLS
LABEL UNARCLOOP
   VAR arcfil VAR arc&'VAR cnt'
   VAR subdir VAR sub&'VAR cnt'
   INVOKE "&'VAR arcprg'" "&'VAR arccmd' &'VAR arcfil' &'VAR subdir'"
   INCVAR cnt
   IF VAR cnt NE VAR arclim
        GOTO UNARCLOOP
   ENDIF

   REM BUILD MENU
   CLS
   GOTO BUILD&'VAR scr'

LABEL BUILDCGA
LABEL BUILDEGA
LABEL BUILDVGA
   INVOKE "C:\MENU\MAKEMM.EXE" "MENU CGA"
   GOTO ENDBUILD

LABEL BUILDMONO
   INVOKE "C:\MENU\MAKEMM.EXE" "MENU MONO"
   GOTO ENDBUILD

LABEL BUILDEGAMON
   INVOKE "C:\MENU\MAKEMM.EXE" "MENU EGAMON"

LABEL ENDBUILD

   REM CREATE INTERIM AUTOEXEC.BAT FILE

   CHDIR C:\
   CREATE C:\AUTOEXEC.BAT

   IF VAR dosnam EQ DOS321
        APPEND C:\AUTOEXEC.BAT "ECHO OFF"
   ELSE
        APPEND C:\AUTOEXEC.BAT "@ECHO OFF"
   ENDIF

   APPEND C:\AUTOEXEC.BAT CLS "VERIFY ON"
   APPEND C:\AUTOEXEC.BAT "PATH C:\;C:\DOS;C:\MENU;C:\UTILS"
   APPEND C:\AUTOEXEC.BAT &'VAR clk'
   APPEND C:\AUTOEXEC.BAT CLS MENU\MENU

   REM CREATE CONFIG.SYS FILE
   CREATE C:\CONFIG.SYS

   APPEND C:\CONFIG.SYS BREAK=ON FILES=20 BUFFERS=20
   APPEND C:\CONFIG.SYS DEVICE=C:\DOS\ANSI.SYS LASTDRIVE=Z
   APPEND C:\CONFIG.SYS "SHELL=C:\DOS\COMMAND.COM C:\DOS /E:256 /P"

   IF VAR dosnam EQ DOS321
        APPEND C:\CONFIG.SYS COUNTRY=061
   ELSE
        APPEND C:\CONFIG.SYS COUNTRY=061,,C:\DOS\COUNTRY.SYS
   ENDIF

   EXISTS C:\DMDRVR.BIN
   IF SYS IO EQ 0
        APPEND C:\CONFIG.SYS DEVICE=C:\DMDRVR.BIN
   ENDIF

   EXISTS C:\MULTIVOL.SYS
   IF SYS IO EQ 0
        APPEND C:\CONFIG.SYS DEVICE=C:\MULTIVOL.SYS
   ENDIF

   REM CHECK OF CLOCK SOFTWARE DELETED. SCRIPT ASSUMES MENU ENTRIES CORRECT.

   REM DELETE ZIP FILES AND INSTALLATION SUPPORT FILES

   SHELL "DEL C:\*.&'VAR arcext'"
   SHELL "DEL &'VAR arcprg'"
   SHELL "DEL C:\SCAN.EXE"
   SHELL "DEL C:\*.BAK"

   REM BACKUP CONFIG.SYS AND AUTOEXEC.BAT FILES, MAKE R/O AND HIDDEN

   SHELL "COPY AUTOEXEC.BAT AUTOEXEC.SAV"
   SHELL "COPY CONFIG.SYS CONFIG.SAV"
   SHELL "C:\UTILS\ATTR +R +H *.SAV"

   REM SET OTHER IMPORTANT FILES TO R/O

   SHELL "C:\UTILS\ATTR +R C:\DOS\*.*"
   SHELL "C:\UTILS\ATTR +R C:\DOS"
   SHELL "C:\UTILS\ATTR +R C:\COMMAND.COM"

   EXISTS C:\DMDRVR.BIN
   IF SYS IO EQ 0
        SHELL "C:\UTILS\ATTR +R C:\DMDRVR.BIN
   ENDIF

   EXISTS C:\MULTIVOL.SYS
   IF SYS IO EQ 0
        SHELL "C:\UTILS\ATTR +R C:\MULTIVOL.SYS
   ENDIF

   CLS
   NOTIFY "OPTIMIZING DISK"
   INVOKE "C:\VOPT.EXE" ""
   NOTIFY "DELETING OPTIMIZER"
   SHELL "DEL C:\VOPT.EXE"

   NOTIFY REBOOTING
   REBOOT
